rootProject.name = 'fritzbox-report-mail'

include 'model'
include 'parser'
include 'db-loader'
include 'app'

def includeBuildIfExists(path) {
    def dir = file(path)
    if(dir.exists()) {
        logger.info "Including project ${dir} into build"
        includeBuild dir
    }
}

//includeBuildIfExists '../simple-jdbc'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        libs {
            version('mime4j', '0.8.11')
            version('junitJupiter', '5.10.1')

            library('exasolJdbc', 'com.exasol:exasol-jdbc:24.1.0')
            library('kryo', 'com.esotericsoftware:kryo:5.6.0')
            library('fastcsv', 'de.siegmar:fastcsv:3.1.0')
            library('simpleJdbc', 'org.itsallcode:simple-jdbc:0.6.1')
            library('mime4j.core', 'org.apache.james', 'apache-mime4j-core').versionRef('mime4j')
            library('mime4j.dom', 'org.apache.james', 'apache-mime4j-dom').versionRef('mime4j')
            library('mime4j.mboxIterator', 'org.apache.james', 'apache-mime4j-mbox-iterator').versionRef('mime4j')
            library('jsoup', 'org.jsoup:jsoup:1.17.2')
            library('jakartaMail', 'com.sun.mail:jakarta.mail:2.0.1')
            
            library('assertj', 'org.assertj:assertj-core:3.25.3')
            library('junitPioneer', 'org.junit-pioneer:junit-pioneer:2.2.0')
            library('equalsverifier', 'nl.jqno.equalsverifier:equalsverifier:3.16.1')
            library('tostringverifier', 'com.jparams:to-string-verifier:1.4.8')
            library('hamcrest', 'org.hamcrest:hamcrest:2.2')
            library('hamcrestResultSetMatcher', 'com.exasol:hamcrest-resultset-matcher:1.6.5')
            library('mockito', 'org.mockito:mockito-core:5.11.0')
            library('exasolTestcontainers', 'com.exasol:exasol-testcontainers:7.1.0')
            library('slf4jLogger', 'org.slf4j:slf4j-jdk14:1.7.36')
            
            bundle('testUtils', ['assertj', 'junitPioneer', 'equalsverifier', 'tostringverifier', 'hamcrest', 'mockito', 'hamcrestResultSetMatcher'])
            bundle('testcontainers', ['exasolTestcontainers'])
            bundle('mime4j', ['mime4j.core', 'mime4j.dom', 'mime4j.mboxIterator'])
        }
    }
}
