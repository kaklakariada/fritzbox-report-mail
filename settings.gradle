rootProject.name = 'fritzbox-report-mail'

include 'model'
include 'parser'
include 'db-loader'
include 'app'

def includeBuildIfExists(path) {
    def dir = file(path)
    if(dir.exists()) {
        logger.info "Including project ${dir} into build"
        includeBuild dir
    }
}

includeBuildIfExists '../simple-jdbc'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        libs {
            version('mime4j', '0.8.9')
            version('junitJupiter', '5.10.0')

            library('exasolJdbc', 'com.exasol:exasol-jdbc:7.1.20')
            library('kryo', 'com.esotericsoftware:kryo:5.5.0')
            library('fastcsv', 'de.siegmar:fastcsv:2.2.2')
            library('simpleJdbc', 'org.itsallcode:simple-jdbc:0.3.0')
            library('mime4j.core', 'org.apache.james', 'apache-mime4j-core').versionRef('mime4j')
            library('mime4j.dom', 'org.apache.james', 'apache-mime4j-dom').versionRef('mime4j')
            library('mime4j.mboxIterator', 'org.apache.james', 'apache-mime4j-mbox-iterator').versionRef('mime4j')
            library('jsoup', 'org.jsoup:jsoup:1.16.1')
            library('jakartaMail', 'com.sun.mail:jakarta.mail:2.0.1')
            
            library('assertj', 'org.assertj:assertj-core:3.24.2')
            library('junitPioneer', 'org.junit-pioneer:junit-pioneer:2.1.0')
            library('equalsverifier', 'nl.jqno.equalsverifier:equalsverifier:3.15.2')
            library('hamcrest', 'org.hamcrest:hamcrest-all:1.3')
            library('mockito', 'org.mockito:mockito-core:5.6.0')
            library('exasolTestcontainers', 'com.exasol:exasol-testcontainers:6.6.2')
            library('testcontainersJupiter', 'org.testcontainers:junit-jupiter:1.19.1')
            
            bundle('testUtils', ['assertj', 'junitPioneer', 'equalsverifier', 'hamcrest', 'mockito'])
            bundle('testcontainers', ['exasolTestcontainers', 'testcontainersJupiter'])
            bundle('mime4j', ['mime4j.core', 'mime4j.dom', 'mime4j.mboxIterator'])
        }
    }
}
