rootProject.name = 'fritzbox-report-mail'

include 'model'
include 'parser'
include 'db-loader'
include 'app'

def includeBuildIfExists(path) {
    def dir = file(path)
    if(dir.exists()) {
        logger.info "Including project ${dir} into build"
        includeBuild dir
    }
}

//includeBuildIfExists '../simple-jdbc'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        libs {
            version('mime4j', '0.8.11')
            version('junitJupiter', '5.10.1')

            library('exasolJdbc', 'com.exasol:exasol-jdbc:24.1.2')
            library('kryo', 'com.esotericsoftware:kryo:5.6.2')
            library('fastcsv', 'de.siegmar:fastcsv:3.3.1')
            library('simpleJdbc', 'org.itsallcode:simple-jdbc:0.7.1')
            library('mime4j.core', 'org.apache.james', 'apache-mime4j-core').versionRef('mime4j')
            library('mime4j.dom', 'org.apache.james', 'apache-mime4j-dom').versionRef('mime4j')
            library('mime4j.mboxIterator', 'org.apache.james', 'apache-mime4j-mbox-iterator').versionRef('mime4j')
            library('jsoup', 'org.jsoup:jsoup:1.18.1')
            library('jakartaMail', 'com.sun.mail:jakarta.mail:2.0.1')
            
            library('assertj', 'org.assertj:assertj-core:3.26.3')
            library('junitPioneer', 'org.junit-pioneer:junit-pioneer:2.3.0')
            library('equalsverifier', 'nl.jqno.equalsverifier:equalsverifier:3.17.1')
            library('tostringverifier', 'com.jparams:to-string-verifier:1.4.8')
            library('hamcrest', 'org.hamcrest:hamcrest:3.0')
            library('hamcrestResultSetMatcher', 'com.exasol:hamcrest-resultset-matcher:1.7.0')
            library('mockito', 'org.mockito:mockito-core:5.14.1')
            library('exasolTestcontainers', 'com.exasol:exasol-testcontainers:7.1.1')
            library('slf4jLogger', 'org.slf4j:slf4j-jdk14:2.0.16')
            library('slf4jApi', 'org.slf4j:slf4j-api:2.0.16')
            
            bundle('testUtils', ['assertj', 'junitPioneer', 'equalsverifier', 'tostringverifier', 'hamcrest', 'mockito', 'hamcrestResultSetMatcher'])
            bundle('testcontainers', ['exasolTestcontainers'])
            bundle('mime4j', ['mime4j.core', 'mime4j.dom', 'mime4j.mboxIterator'])
        }
    }
}
